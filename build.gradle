plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "org.jetbrains.dokka" version "0.10.0"
    id "io.codearte.nexus-staging" version "0.21.1"
}

apply from: rootProject.file('common.gradle')

// Dokka aggregation을 위한 library
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

// Dokka aggregation
dokka {
    outputDirectory = "$rootDir/docs/api"
    outputFormat = "html"

    configuration {
        moduleName = rootProject.name
        jdkVersion = 8
        noStdlibLink = false

        subprojects.forEach { p ->
            def projPath = new File(p.projectDir, "/src/main/kotlin")
            def relativePath = rootDir.toPath().relativize(projPath.toPath()).toString()

            sourceLink {
                path = projPath
                url = "https://github.com/koalanlp/koalanlp/blob/master/$relativePath"
                lineSuffix = "#L"
            }

            sourceRoot {
                path = projPath
            }
        }

        includes = ['docs/index.md', *subprojects.collect { "${it.projectDir}/index.md".toString() }]
    }
}

// For staging plugin.
ext.nexusUsername = project.properties.'ossrhUsername' as String ?: 'Foo'
ext.nexusPassword = project.properties.'ossrhPassword' as String ?: 'Foo'
