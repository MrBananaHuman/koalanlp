task unidocJar(type: Jar, dependsOn: dokka) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task testDependencyJar(type: Jar, dependsOn: compileTestKotlin){
    classifier 'tests'
    from sourceSets.test.output
    exclude '**/*CoreSpec*'
    exclude '*.zip'
}

def nexusUsername = project.properties.'ossrhUsername' as String ?: 'Foo'
def nexusPassword = project.properties.'ossrhPassword' as String ?: 'Foo'
def snapshotURL = "https://oss.sonatype.org/content/repositories/snapshots/" 
def publishURL = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

publishing {
    repositories {
        maven { 
            url = project.version.endsWith('SNAPSHOT') ? snapshotURL : publishURL
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }
    }
    publications {
        KoalaNLP(MavenPublication) {
            from components.java
            artifact tasks.kotlinSourcesJar
            artifact unidocJar

            if (project.name.endsWith("-core")) {
                artifact testDependencyJar
            }

            pom {
                name = project.name
                groupId = project.group
                artifactId = project.name
                version = project.version
                description = "KoalaNLP는 한국어 처리의 통합 인터페이스를 지향하는 Java/Kotlin/Scala Library의 묶음입니다."

                packaging "jar"
                url = "http://koalanlp.github.io/koalanlp"

                scm {
                    connection = "scm:git:git@github.com:koalanlp/koalanlp.git"
                    developerConnection = "scm:git:git@github.com:koalanlp/koalanlp.git"
                    url = "https://github.com/koalanlp/koalanlp"
                }

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://tldrlegal.com/license/mit-license"
                    }
                }

                developers {
                    developer {
                        id = "nearbydelta"
                        name = "Bugeun Kim"
                        url = "http://github.com/nearbydelta"
                    }
                }
            }
        }
    }
}

dokka {
    outputFormat = "html"
    outputDirectory = javadoc.destinationDir
    configuration {
        reportUndocumented = true
        noStdlibLink = false

        sourceLink {
            path = "src/main/kotlin"
            url = "https://github.com/koalanlp/koalanlp/blob/master/${projectDir.name}/src/main/kotlin"
            lineSuffix = "#L"
        }

        if (!project.name.endsWith('-core')) {
            externalDocumentationLink {
                url = new URL("https://koalanlp.github.io/koalanlp/api/koalanlp/")
                packageListUrl = new URL("https://koalanlp.github.io/koalanlp/api/koalanlp/package-list")
            }
        }

        includes = ["index.md"]
    }
}

if (!project.version.endsWith('SNAPSHOT')) {
    signing {
        sign publishing.publications.KoalaNLP
    }
}
